apply plugin: 'java'

buildscript {

    apply from: '../dependencies.gradle'

    ext {
        //dependency versions
        ehibasicsVersion = '1.1.0'

        // dependencies
        ehibasicsDependency = [ group: 'ch.ehi', name: 'ehibasics', version: ehibasicsVersion ]
    }

    repositories {
        maven {
            url "http://jars.interlis.ch"
        }
        maven {
            url "http://download.osgeo.org/webdav/geotools/"
        }
        mavenCentral()
    }

    dependencies {
        // TODO no version lock
        classpath files('../build/libs/gretl-1.0.4-SNAPSHOT.jar')

        classpath ilivalidatorDependency
        classpath ehibasicsDependency
    }
}

repositories {
    mavenCentral()
}

sourceSets {
    testIntegration
    testSystem
}

dependencies {
    compile 'org.testng:testng:6.9.6'
    compile 'junit:junit:4.12'

    testIntegrationCompile junitDependency
    testSystemCompile junitDependency
}

task testIntegration(type: Test) {
    group = 'Verification'
    description = 'Runs integration tests'

    testClassesDirs = sourceSets.testIntegration.output.classesDirs
    classpath = sourceSets.testIntegration.runtimeClasspath
}

task testSystem(type: Test) {
    group = 'Verification'
    description = 'Runs system tests including Jenkins'

    testClassesDirs = sourceSets.testSystem.output.classesDirs
    classpath = sourceSets.testSystem.runtimeClasspath
}